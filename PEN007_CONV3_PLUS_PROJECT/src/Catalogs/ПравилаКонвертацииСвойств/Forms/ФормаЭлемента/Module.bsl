//@skip-check module-self-reference
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура PEN007_ПриСозданииНаСервереПосле(Отказ, СтандартнаяОбработка)

	PEN007_Заголовок = "Перейти к правилу конвертации объекта";
	// Команда PEN007_ОткрытьФормуПКО
	PEN007_ИмяКоманды = "КомандаОткрытьФормуПКО";
	ПФИВызовСервера.СоздатьКоманду(ЭтотОбъект, PEN007_ИмяКоманды, PEN007_Заголовок, "PEN007_ОткрытьФормуПКО");
	
	// Кнопка PEN007_КнопкаОткрытьФормуПКО
	PEN007_Родитель = ЭтотОбъект.Элементы.Найти("ГруппаНастройкаПравилаКонвертацииСвойства");
	Если PEN007_Родитель <> Неопределено Тогда
		PEN007_ЭлементПеред = ЭтотОбъект.Элементы.Найти("УказыватьПравилоКонвертацииСвойстваСтрокой");
		PEN007_СвойстваЭлемента = Новый Структура();
		PEN007_СвойстваЭлемента.Вставить("Картинка", БиблиотекаКартинок.ПереместитьВправо);
		PEN007_СвойстваЭлемента.Вставить("Отображение", ОтображениеКнопки.Картинка);
		ПФИВызовСервера.Кнопка(ЭтотОбъект, "PEN007_КнопкаОткрытьФормуПКО", PEN007_Родитель, PEN007_Заголовок,
			"КомандаОткрытьФормуПКО", 1, PEN007_СвойстваЭлемента, PEN007_ЭлементПеред);
	КонецЕсли;

КонецПроцедуры

#КонецОбласти


#Область КомандыФормы

//@skip-check module-unused-method
//@skip-check extension-variable-prefix
&НаКлиенте
Процедура PEN007_ОткрытьФормуПКО(Команда)

	Если ПустаяСтрока(Объект.ПравилоКонвертацииСвойства) Тогда
		Возврат;
	КонецЕсли;

	ТипПКО = PEN007_ОбщегоНазначенияВызовСервера.ТипПравилаКонвертацииПоСвойству(
		Объект.СвойствоКонфигурацииКорреспондента);

	СправочникСсылка = PEN007_ОбщегоНазначенияВызовСервера.СсылкаНаПКО(Объект.ПравилоКонвертацииСвойства, ТипПКО);
	ПараметрыФормы = Новый Структура();
	ПараметрыФормы.Вставить("Ключ", СправочникСсылка);
	ИмяФормыПравила = СтрШаблон("Справочник.%1.ФормаОбъекта", ТипПКО);
	ОткрытьФорму(ИмяФормыПравила, ПараметрыФормы, ЭтотОбъект);

КонецПроцедуры

#КонецОбласти


#Область СлужебныеПроцедурыИФункции


#КонецОбласти